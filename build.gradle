// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.guava:guava:22.0'
        classpath 'com.android.tools.build:gradle:3.1.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task buildScriptDependencies(type: DependencyReportTask) {
    configurations = project.buildscript.configurations
}

// Starts local server, runs all instrumented tests then stops the server.
task runTestsOnLocalServer {
    dependsOn 'startBackend', 'runConnectedTests', 'stopBackend'
    doLast {
        println 'runTestsOnLocalServer done!'
    }
}

task startBackend {
    dependsOn ":backend:appengineRun"
    project(":backend").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }
    doLast {
        println "Backend started!"
    }
}

// Runs all instrumented tests
task runConnectedTests {
    shouldRunAfter 'startBackend'
    dependsOn ':app:connectedCheck'
    doLast {
        println 'Instrumented test done!'
    }
}

task stopBackend {
    shouldRunAfter 'runConnectedTests'
    dependsOn ':backend:appengineStop'
    doLast {
        println 'Backend stopped!'
    }
}